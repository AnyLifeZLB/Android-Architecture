if (isModule.toBoolean()) { //确定是不是组件开发模式
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion

    defaultConfig {
        if (isModule.toBoolean()) {
            applicationId "com.anylife.module_ai"  //制定 applicationId
        }

        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion

        //配置使用ARouter
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [moduleName: project.getName()]
            }
        }

        ndk {
            //设置支持的SO库架构
            abiFilters 'armeabi'      //, 'x86', 'armeabi-v7a', 'x86_64', 'arm64-v8a'
        }

    }

    sourceSets {
        main {

            if (isModule.toBoolean()) {
                manifest.srcFile 'src/main/module/AndroidManifest.xml'
            } else {
                manifest.srcFile 'src/main/AndroidManifest.xml'
                //集成开发模式下排除debug文件夹中的所有Java文件
                java {
                    exclude 'debug/**'
                }
            }

        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility rootProject.ext.sourceCompatibility
        targetCompatibility rootProject.ext.targetCompatibility
    }

}


dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation project(':baselib')

    implementation "com.android.support:appcompat-v7:$rootProject.ext.supportLibraryVersion"
    implementation "com.android.support:recyclerview-v7:$rootProject.ext.supportLibraryVersion"
    annotationProcessor "com.google.dagger:dagger-compiler:$rootProject.ext.daggerVersion"
    annotationProcessor "com.google.dagger:dagger-android-processor:$rootProject.ext.daggerVersion"

    implementation 'com.android.support.constraint:constraint-layout:1.1.3'

    annotationProcessor 'com.alibaba:arouter-compiler:1.1.4'

    implementation 'com.github.bumptech.glide:glide:4.7.1'    // !!!!!!

    implementation 'io.reactivex.rxjava2:rxjava:2.1.16'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.2'

}
