if (isModule.toBoolean()) {
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion

    defaultConfig {
        if (isModule.toBoolean()) {
            applicationId "com.zenglb.framework.module_main"
        }

        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        //支持VectorDrawable
        vectorDrawables.useSupportLibrary = true

        //配置使用ARouter
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [AROUTER_MODULE_NAME: project.getName()]
            }
        }

        //设置支持的SO abi库架构支持
        ndk {
            abiFilters  'armeabi-v7a'
        }
    }


    sourceSets {
        main {
            if (isModule.toBoolean()) {
                //组件开发模式下排除debug文件夹中的所有Java文件
                manifest.srcFile 'src/main/module/AndroidManifest.xml'
            } else {
                manifest.srcFile 'src/main/AndroidManifest.xml'
                //集成开发模式下排除debug文件夹中的所有Java文件
                java {
                    exclude 'debug/**'
                }
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility rootProject.ext.sourceCompatibility
        targetCompatibility rootProject.ext.targetCompatibility
    }
}


dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation "com.android.support:appcompat-v7:$rootProject.ext.supportLibraryVersion"
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'

    implementation 'com.android.support:support-v4:28.0.0'
    annotationProcessor 'com.alibaba:arouter-compiler:1.2.2'
    implementation project(':baselib')

    // if you use the support libraries
    annotationProcessor "com.google.dagger:dagger-compiler:$rootProject.ext.daggerVersion"
    annotationProcessor "com.google.dagger:dagger-android-processor:$rootProject.ext.daggerVersion"

    implementation 'com.jakewharton.rxbinding2:rxbinding:2.0.0'     //Platform bindings
    implementation 'com.jakewharton.rxbinding2:rxbinding-support-v4:2.0.0'
    implementation 'com.jakewharton.rxbinding2:rxbinding-appcompat-v7:2.0.0'

    //自定义相机,Maybe not good
    implementation([
            'com.camerakit:camerakit:1.0.0-beta3.10',
            'com.camerakit:jpegkit:0.1.0',
            'org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.3.0',
            'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.0.0'
    ])

    implementation 'anylife.downloadinstaller:downloadInstaller:1.1.1'
    implementation 'com.zhihu.android:matisse:0.4.3'  //图片视频选择，不要再用PhotoPicker 了

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'


}
