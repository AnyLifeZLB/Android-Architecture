if (isModule.toBoolean()) {
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
//    resourcePrefix "module_news" //防止资源冲突

    defaultConfig {

        if (isModule.toBoolean()) {
            applicationId "com.zenglb.framework.news"  //制定 applicationId
        }

        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 2
        versionName "1.1"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        //配置使用ARouter
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [moduleName: project.getName()]
            }
        }

        //设置支持的SO abi库架构支持
        ndk {
            abiFilters 'armeabi-v7a'
        }

    }

    signingConfigs {
        anylifezlbConfig {
            storeFile file("NewsKeyStore")
            storePassword "NewsKeyStore"
            keyAlias "NewsKeyStore"
            keyPassword "NewsKeyStore"
        }
    }


    buildTypes {
        release {
            signingConfig signingConfigs.anylifezlbConfig
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }


    sourceSets {
        main {
            if (isModule.toBoolean()) {
                manifest.srcFile 'src/main/module/AndroidManifest.xml'
            } else {
                manifest.srcFile 'src/main/AndroidManifest.xml'
                //集成开发模式下排除debug文件夹中的所有Java文件
                java {
                    exclude 'debug/**'
                }
            }
        }
    }

    compileOptions {
        sourceCompatibility rootProject.ext.sourceCompatibility
        targetCompatibility rootProject.ext.targetCompatibility
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "com.android.support:support-v4:$rootProject.ext.supportLibraryVersion"

    testImplementation 'junit:junit:4.12'
    implementation project(':baselib')

    annotationProcessor "com.google.dagger:dagger-compiler:$rootProject.ext.daggerVersion"
    annotationProcessor "com.google.dagger:dagger-android-processor:$rootProject.ext.daggerVersion"

    annotationProcessor 'com.alibaba:arouter-compiler:1.1.4'
}
